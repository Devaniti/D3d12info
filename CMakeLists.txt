cmake_minimum_required(VERSION 3.17)

project(D3d12info LANGUAGES CXX)
set(CMAKE_CXX_STANDARD 20)
file(GLOB CPP_FILES "Src/*.cpp")
file(GLOB HPP_FILES "Src/*.hpp")
add_executable(D3d12info ${CPP_FILES} ${HPP_FILES})

set_property(DIRECTORY ${PROJECT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT "D3d12info")
target_compile_options(D3d12info PRIVATE /W4 /wd4100 /wd4189)
add_compile_definitions(UNICODE _UNICODE)

set(RAPIDJSON_NATVIS_FILE "Src/ThirdParty/rapidjson/contrib/natvis/rapidjson.natvis")
target_sources(D3d12info PRIVATE ${RAPIDJSON_NATVIS_FILE})
source_group("ThirdParty" FILES ${RAPIDJSON_NATVIS_FILE})
target_precompile_headers(D3d12info PRIVATE "Src/pch.hpp")

set(AGS_DIRECTORY "" CACHE STRING "Path to AMD GPU Services (AGS) library - \"ags_lib\" directory. Leave empty to compile without AGS.")
if(AGS_DIRECTORY)
    if(EXISTS "${AGS_DIRECTORY}/inc/amd_ags.h")
        message(STATUS "AMD AGS library used from \"${AGS_DIRECTORY}\".")
        add_compile_definitions(USE_AGS)
        target_include_directories(D3d12info PRIVATE "${AGS_DIRECTORY}/inc")
        target_link_libraries(D3d12info PRIVATE "${AGS_DIRECTORY}/lib/amd_ags_x64.lib")
    else()
        message(FATAL_ERROR "AMD AGS library not found - cannot find file \"${AGS_DIRECTORY}/inc/amd_ags.h\".")
    endif()
else()
    message(STATUS "AMD AGS library not used.")
endif()

set(NVAPI_DIRECTORY "" CACHE STRING "Path to NVAPI library. Leave empty to compile without NVAPI.")
if(NVAPI_DIRECTORY)
    if(EXISTS "${NVAPI_DIRECTORY}/nvapi.h")
        message(STATUS "NVAPI library used from \"${NVAPI_DIRECTORY}\".")
        add_compile_definitions(USE_NVAPI)
        target_include_directories(D3d12info PRIVATE "${NVAPI_DIRECTORY}")
        target_link_libraries(D3d12info PRIVATE "${NVAPI_DIRECTORY}/amd64/nvapi64.lib")
    else()
        message(FATAL_ERROR "NVAPI library not found - cannot find file \"${NVAPI_DIRECTORY}/nvapi.h\".")
    endif()
else()
    message(STATUS "NVAPI library not used.")
endif()

set(VULKAN_SDK_DIRECTORY "" CACHE STRING "Path to Vulkan SDK - subdirectory like \"1.3.250.0\". Leave empty to compile without Vulkan.")
if(VULKAN_SDK_DIRECTORY)
    if(EXISTS "${VULKAN_SDK_DIRECTORY}/Include/vulkan/vulkan.h")
        message(STATUS "Vulkan SDK used from \"${VULKAN_SDK_DIRECTORY}\".")
        add_compile_definitions(USE_VULKAN)
        target_include_directories(D3d12info PRIVATE "${VULKAN_SDK_DIRECTORY}/Include")
    else()
        message(FATAL_ERROR "Vulkan SDK not found - cannot find file \"${VULKAN_SDK_DIRECTORY}/Include/vulkan/vulkan.h\".")
    endif()
else()
    message(STATUS "Vulkan SDK not used.")
endif()

